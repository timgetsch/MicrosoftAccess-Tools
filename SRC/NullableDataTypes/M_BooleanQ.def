Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = False
Attribute VB_Exposed = False
'--------------------------------------------------------------------------------------
' BooleanQ v0.1.1
' (c) Tim Getsch
'
' BooleanQ is the nullable version of Boolean
'
' @class BooleanQ
' @author timgetsch@commitcs.com
' @license MIT (http://www.opensource.org/licenses/mit-license.php)
'---------------------------------------------------------------------------------------
Option Compare Database
Option Explicit

Private m_HasValue As Boolean
Private m_Value As Boolean

Public Property Get HasValue() As Boolean
    HasValue = m_HasValue
End Property

Public Property Get Value() As Boolean
    If Not m_HasValue Then
        Err.Raise 1, "BooleanQ.Value", "Nullable object has no value."
    Else
        Value = m_Value
    End If
End Property

Public Function GetValueOrDefault(Optional DefaultValue As Boolean = False) As Boolean
    If Me.HasValue Then
        GetValueOrDefault = Me.Value
    Else
        GetValueOrDefault = DefaultValue
    End If
End Function

Public Property Let SetValue(Value As Variant)
Attribute SetValue.VB_UserMemId = 0
    m_HasValue = False
    If Not IsNull(Value) Then
        m_Value = CBool(Value)
        m_HasValue = True
    End If
End Property

Public Property Get SetValue() As Variant
    If m_HasValue Then
        SetValue = m_Value
    Else
        SetValue = Null
    End If
End Property
