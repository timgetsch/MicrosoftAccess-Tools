'---------------------------------------------------------------------------------------
'          __    _____   ______       ___       _____ ____    __
'         / /   /  _/ | / / __ \     |__ \     / ___// __ \  / /
'        / /    / //  |/ / / / /     __/ /     \__ \/ / / / / /
'       / /____/ // /|  / /_/ /     / __/     ___/ / /_/ / / /___
' _____/_____/___/_/ |_/\___\_\____/____/____/____/\___\_\/_____/
'/_____/                     /_____/   /_____/
'
' (c) Tim Getsch
' @author timgetsch@commitcs.com
' @license MIT (http://www.opensource.org/licenses/mit-license.php)
'---------------------------------------------------------------------------------------
Option Compare Database
Option Explicit

Public Function Init_BaseModules(NewClassName As String)
10        On Error GoTo PROC_ERROR
          
20        AddReferenceADODB ' This is needed by cls_LINQ_Base
30        AddModuleIfMissing "mod_LINQ_2_SQL_LogError"
40        AddModuleIfMissing "cls_LINQ_2_SQL__StringBuilder"
50        AddModuleIfMissing "cls_LINQ_2_SQL__SqlGenerator"
60        AddModuleIfMissing "cls_LINQ_2_SQL__BaseClass"
70        AddModuleIfMissing "cls_LINQ_2_SQL__Databases"
80        AddModuleIfMissing "cls_LINQ_2_SQL_TEMPLATE", NewClassName
90        AddModuleIfMissing "mod_LINQ_2_SQL_Shared"

PROC_EXIT:
100       Exit Function

PROC_ERROR:
110       LogError "_LINQ_2_SQL_Templates_Init", "Init_LINQ", Erl, Err.Number, Err.Description
120       Resume PROC_EXIT
130       Resume
End Function

Private Sub AddModuleIfMissing(ModuleName As String, Optional NewName As String = "")
10        On Error GoTo PROC_ERROR

20        If NewName = "" Then NewName = ModuleName
          
30        If Not IsObjectInCol(CurrentProject.AllModules, NewName) Then
40            DoCmd.CopyObject CurrentDb.Name, NewName, acModule, ModuleName
50        End If

PROC_EXIT:
60        Exit Sub

PROC_ERROR:
70        LogError "_LINQ_2_SQL_Templates_Init", "AddModuleIfMissing", Erl, Err.Number, Err.Description
80        Resume PROC_EXIT
90        Resume
End Sub

Public Function IsObjectInCol(col As Object, ByVal Index) As Boolean
10        On Error Resume Next
          Dim val As Object
20        Set val = col.Item(Index)
30        IsObjectInCol = Err.Number = 0
40        Err.Clear
End Function

Private Sub AddReferenceIfNecessary(Guid As String, Major As Long, Minor As Long)
10        On Error GoTo PROC_ERROR

          Dim r As VBIDE.Reference
20        For Each r In CurrentVBProject.References
30            If r.Guid = Guid Then
40                Exit Sub
50            End If
60        Next
          
70        CurrentVBProject.References.AddFromGuid Guid, Major, Minor

PROC_EXIT:
80        Exit Sub

PROC_ERROR:
90        LogError "_LINQ_2_SQL_Templates_Init", "AddReferenceIfNecessary", Erl, Err.Number, Err.Description
100       Resume PROC_EXIT
110       Resume
End Sub

Private Function AddReferenceADODB()
10        On Error GoTo PROC_ERROR

20        AddReferenceIfNecessary "{2A75196C-D9EB-4129-B803-931327F72D5C}", 2, 8

PROC_EXIT:
30        Exit Function

PROC_ERROR:
40        LogError "_LINQ_2_SQL_Templates_Init", "AddReferenceADODB", Erl, Err.Number, Err.Description
50        Resume PROC_EXIT
60        Resume
End Function